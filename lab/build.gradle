buildscript {
    ext {
        springBootVersion = "2.3.1.RELEASE"
        jaxbAPIVersion = "2.3.0"
        simpleJndiVersion = "0.11.4.1"
        springCloudStarterOauth2="2.2.0.RELEASE"
        easyMockVersion = "4.1"
        jmonVersion = "2.82"
        wavefrontVersion="2.0.0-SNAPSHOT"
    }

    repositories {
        mavenCentral()
        jcenter()
    }

}

plugins {
    id 'org.springframework.boot' version "$springBootVersion"
}

allprojects {
    apply plugin: 'maven-publish'

    group = 'io.pivotal.education.core-spring'
    version = '5.0.c.RELEASE'
}

subprojects {

    apply plugin: 'java'
    apply plugin: "io.spring.dependency-management"

    repositories {
        maven {
            url = 'https://repo.maven.apache.org/maven2'
        }
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
        }
    }

    dependencies {
        implementation "org.springframework.boot:spring-boot-starter"
        implementation "org.springframework.boot:spring-boot-starter-jdbc"
        implementation "org.hsqldb:hsqldb"
        implementation "javax.xml.bind:jaxb-api:$jaxbAPIVersion"

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    sourceCompatibility = '11'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
    }

    apply plugin: 'idea'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}
